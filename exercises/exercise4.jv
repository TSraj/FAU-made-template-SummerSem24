  pipeline WeatherPipeline {
  
  block WeatherExtractor oftype HttpExtractor {
    url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
  }
  
  block ZipInterpreter oftype ArchiveInterpreter {
    archiveType: "zip";
  }
  
  block WeatherFilePicker oftype FilePicker {
    path: "/data.csv";
  }
  
  block WeatherCSVInterpreter oftype CSVInterpreter {
    delimiter: ';';
  }
  
  block TextInterpreter oftype TextFileInterpreter {}

  block ColDeleter oftype ColumnDeleter {
    delete: [column F, column G, column H, column I];
  }
  
  block ColDataSelector oftype CellRangeSelector {
    select: range A1:K*;
  }
  
    constraint PositiveRange oftype RangeConstraint { lowerBound: 0; lowerBoundInclusive: false;}
    valuetype PositiveInteger oftype integer { constraints: [ PositiveRange ]; }

  block WeatherTableInterpreter oftype TableInterpreter {
    header: false;
    columns: [
      "id" oftype PositiveInteger,
      "producer" oftype text,
      "model" oftype text,
      "month" oftype integer,
      "temperature" oftype decimal,
      "battery_temperature" oftype decimal
    ];
  }
  
  transform TempConvert {
    from tempCelsius oftype decimal;
    to tempFahrenheit oftype decimal;
    tempFahrenheit: (tempCelsius * 9/5) + 32;
  }

  block TransformerA oftype TableTransformer {
    inputColumns: ['temperature'];
    outputColumn: 'temperature';
    use: TempConvert;  // Using the defined transformation
  }

  

  block TransformerB oftype TableTransformer {
    inputColumns: ['battery_temperature'];
    outputColumn: 'battery_temperature';
    use: TempConvert;  
  }

  
  block WeatherLoader oftype SQLiteLoader {
    table: "temperatures";
    file: "./temperatures.sqlite";
    dropTable: false;
  }



  
  WeatherExtractor
  -> ZipInterpreter
  -> WeatherFilePicker
  -> TextInterpreter
  -> WeatherCSVInterpreter
  -> ColDataSelector
  -> ColDeleter
  -> WeatherTableInterpreter
  -> TransformerA
  -> TransformerB
  -> WeatherLoader;
}